buildscript {
    ext.kotlin_version = '1.1.4-2'
}

buildscript {
    repositories {
        mavenLocal() //for local testing of shipkit
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath 'com.netflix.nebula:nebula-release-plugin:6.0.0'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.1.4-2"
        classpath 'org.dm.gradle:gradle-bundle-plugin:0.10.0'
    }
}

apply from: "gradle/ide.gradle"

ext.junitPlatformVersion = '1.0.0-RC2'
ext.junitJupiterVersion  = '5.0.0-RC2'

allprojects {
    group = "org.auxis"
    if (version == "unspecified") {
        version "999-SNAPSHOT"
    }
    if (releaseBuild == "true") {
        println "Project: " + project.name + " in Releasebuild: " + releaseBuild
        apply plugin: 'com.jfrog.bintray'
        apply plugin: 'nebula.nebula-release'
    }
}

subprojects { subproject ->
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
        maven { url 'https://repo.gradle.org/gradle/libs-releases' }
    }

    apply plugin: 'java'
    apply plugin: "maven"

    if (new File("$project.projectDir/bnd.bnd").exists()) {
        apply plugin: 'org.dm.bundle'
        bundle {
            instruction '-include', 'bnd.bnd'
            failOnError = true
        }
    }

    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = "sources"
        from sourceSets.main.allSource
    }

    artifacts {
        archives sourcesJar
    }

    if (releaseBuild == "true") {
        //snapshot.dependsOn =[]
        //install.dependsOn snapshot

        bintray {
            user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
            key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
            configurations = ['archives']
            publish = false
            pkg {
                repo = 'rebaze-oss'
                user = 'tonit'
                userOrg = 'rebaze'
                name = "auxis"
                licenses = ['Apache-2.0']
                labels = ['shipkit']
            }
        }
    }
}
